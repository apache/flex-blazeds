<?xml version="1.0"?>
<!--

  Licensed to the Apache Software Foundation (ASF) under one or more
  contributor license agreements.  See the NOTICE file distributed with
  this work for additional information regarding copyright ownership.
  The ASF licenses this file to You under the Apache License, Version 2.0
  (the "License"); you may not use this file except in compliance with
  the License.  You may obtain a copy of the License at

      http://www.apache.org/licenses/LICENSE-2.0

  Unless required by applicable law or agreed to in writing, software
  distributed under the License is distributed on an "AS IS" BASIS,
  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
  See the License for the specific language governing permissions and
  limitations under the License.

-->
<mx:Application xmlns:mx="http://www.adobe.com/2006/mxml" creationComplete="setUp()">           
    
    <mx:Script>
    <![CDATA[
    	import mx.events.PropertyChangeEvent;
        import qa.utils.TestTask;
        import qa.utils.TestTaskEvent;
        import mx.messaging.Producer;
        import mx.messaging.channels.AMFChannel;
        import mx.messaging.Consumer;   
        import mx.messaging.ChannelSet;
        import mx.messaging.Channel;
        import qa.mxunit.Assert;
        import qa.mxunit.MXUnitManager;
        import mx.messaging.messages.*;
        import mx.messaging.config.*;
        import mx.messaging.events.*;
 
        ////////////////////////////////////////////////////////////////////////////
        //
        // Variables
        //
        ////////////////////////////////////////////////////////////////////////////                          
        
        /**
        * Name of the channel used in the test;
        */
        public var channelName:String = "qa-rtpush-amf"; 
        /**
        *  Consumer instance used in the test
        */           
        public var con:Consumer;         
        
        /**
        *  Name of the destination to be used by the Producer and the Consumer. 
        */
        private var destination:String = "MyJMSTopic";          
        
        /**
        *  Flag to test whether this is the first or second time subscribing. 
        */           
        public var firstAttempt:Boolean = true;
                
        /**
        *  Variable to hold singleton instance of the MXUnitManager.  
        */
        private var manager:MXUnitManager;
        
        /**
        *  Producer instance used in the test
        */       
        public var pro:Producer;
                         
        
        ////////////////////////////////////////////////////////////////////////////
        //
        // Setup method.
        //
        //////////////////////////////////////////////////////////////////////////// 
        
        public function setUp():void {    
                                                                                
            //configure the MXUnitManager           
            manager = MXUnitManager.getInstance();                                                             
            //Call addTests giving it the object that contains all our test functions and an array of test names
            MXUnitManager.addTests(this,["disconnectSubscribeTest"], 15000);                        
        }
        ////////////////////////////////////////////////////////////////////////////
        //
        // Tests.
        //
        ////////////////////////////////////////////////////////////////////////////       
        /**
		* This test tests that if a consumer subscribes to a JMS destination, disconnects from the channel 
		* they are connected over and then subscribes to the destination again that they can receive
		* messages sent to the destination.      
		*/
        public function disconnectSubscribeTest():void {  
            
            //setup the producer
            pro = new Producer();
            pro.destination = destination;
            
            //setup the consumer
            con = new Consumer(); 
            con.destination = destination;
            
            /*
             * Use the chain method to setup event handlers for the producer and push an addAsync method onto the stack. Receiving a 
             * message fault a channel fault or an acknowledgement that the server received the message sent by the producer will 
             * remove the addAsync method from the stack. Unless all addAsync methods have been executed the test will time out so 
             * you want to make sure that at least one of the events will get fired. This call is essentially the same as writing 
             * the following code: 
             * var asyncHandler = manager.currentTest.addAsync(producerEventHandler,{"c":con});
             * pro.addEventListener(ChannelFaultEvent.FAULT, asyncHandler);
             * pro.addEventListener(MessageFaultEvent.FAULT, asyncHandler);
             * pro.addEventListener(MessageAckEvent.ACKNOWLEDGE, asyncHandler);   
            */                                        
            manager.currentTest.chain(pro,[MessageFaultEvent.FAULT,ChannelFaultEvent.FAULT,MessageAckEvent.ACKNOWLEDGE],producerEventHandler);
            
            /*
             * Use the chain method to setup event handlers for the consumer and push an addAsync method onto the stack. Receiving a 
             * message fault a channel fault or a message event will cause the addAsync method to be removed from the stack.
             */
            manager.currentTest.chain(con,[MessageFaultEvent.FAULT,ChannelFaultEvent.FAULT,MessageEvent.MESSAGE],consumerEventHandler);
                        
            /*
             * Setup event listener to listen for property changed events from the Consumer. Use this to tell when the 
             * Consumer has subscribed to the destination.    
             */
            con.addEventListener(PropertyChangeEvent.PROPERTY_CHANGE,conPropertyChanged); 
                                                            
            var channel:Channel = ServerConfig.getChannel(channelName, false);		
 			var cs:ChannelSet = new ChannelSet(); 			
			cs.addChannel(channel);
			
			//set the consumer and producer to use the new channel set then subscribe the consumer
			con.channelSet = cs; 
			pro.channelSet = cs;            
            con.subscribe();                           
            
        }
        /**
        *  Listen for property change events on the consumer so we can tell when the consumer has subscribed.    
        */
        private function conPropertyChanged(event:PropertyChangeEvent):void {
            //make sure the ack is for a subscribe operation
            if(event.property == "subscribed" && con.subscribed)
            {
                //test that the subscribe operation was for the channel we are currently testing                 
                Assert.assertTrue("Wrong channel: " + con.channelSet.currentChannel.id, con.channelSet.currentChannel.id == channelName);                              
                
                if (firstAttempt) 
                { 
	                //send the message
	                var msg1:IMessage = new AsyncMessage();
	                msg1.body = "hello1";
	                pro.send(msg1);
	            } else {
	            	//send the message
                    var msg2:IMessage = new AsyncMessage();
                    msg2.body = "hello2";
                    pro.send(msg2);
	            }              
            } 
        }
        
        /**
        *  Listen for disconnect events. Because we used chain (the same would be true had we used
        *  addAsync), the handler will get called by MXUnit instead of getting called directly by the 
        *  producer. This allows the handler to have an extra parameter that we use to pass along the 
        *  Consumer instance that was created in our test function.   
        */
        public function channelDisconnect(event:ChannelEvent):void {                
            //manager.currentTest.abortAllAsyncFunctions();
            //assert that the consumer is no longer subscribed or connected
            Assert.assertTrue("Consumer shouldn't be subscribed", con.subscribed != true);
            Assert.assertTrue("Consumer shouldn't be connected", con.connected != true);
            if (firstAttempt) 
            {
            	firstAttempt = false;
            	manager.currentTest.chain(con,[MessageFaultEvent.FAULT,ChannelFaultEvent.FAULT,MessageEvent.MESSAGE],consumerEventHandler);
            	con.subscribe(); 
            }
        }
        /**
        *  Listen for events from the producer.    
        */
        public function producerEventHandler(event:Event):void 
        {   
            if (event is MessageAckEvent) {
                trace("Producer received ack for message");    
            } else if (event is ChannelFaultEvent) {
                con.unsubscribe();
                con.disconnect();
                var cfe:ChannelFaultEvent = event as ChannelFaultEvent;                        
                Assert.fail("Channel faulted with following error while sending message: " + cfe.faultDetail);     
            } else if (event is MessageFaultEvent) {
                con.unsubscribe();
                con.disconnect();
                var mfe:MessageFaultEvent = event as MessageFaultEvent;
                Assert.fail("Message fault while sending message: " + mfe.faultDetail);                
            }
        }
        /**
        *  Listen for events from the consumer. 
        */
        public function consumerEventHandler(event:Event):void
        {   
            if (event is ChannelFaultEvent) {
                var cfe:ChannelFaultEvent = event as ChannelFaultEvent;                        
                Assert.fail("Consumer channel faulted with the following error: " + cfe.faultDetail);     
            } else if (event is MessageFaultEvent) {            
                var mfe:MessageFaultEvent = event as MessageFaultEvent;
                Assert.fail("Consumer got the following message fault: " + mfe.faultDetail);                
            } else if (event is MessageEvent) {                     
                var me:MessageEvent = event as MessageEvent;
                //get the message body from the message and store it.
                var result:String = me.message.body.toString();
                //assert we got the correct message
                if (firstAttempt) 
                {
                    Assert.assertEquals("Wrong message: " + result, "hello1", result);	
                } else {
                	Assert.assertEquals("Wrong message: " + result, "hello2", result);
                }                   
                manager.currentTest.chain(con,[ChannelEvent.DISCONNECT],channelDisconnect);                         
            }
            con.disconnect();
        }
       
    ]]>
    </mx:Script>           
</mx:Application>