<?xml version="1.0"?>
<!--

  Licensed to the Apache Software Foundation (ASF) under one or more
  contributor license agreements.  See the NOTICE file distributed with
  this work for additional information regarding copyright ownership.
  The ASF licenses this file to You under the Apache License, Version 2.0
  (the "License"); you may not use this file except in compliance with
  the License.  You may obtain a copy of the License at

      http://www.apache.org/licenses/LICENSE-2.0

  Unless required by applicable law or agreed to in writing, software
  distributed under the License is distributed on an "AS IS" BASIS,
  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
  See the License for the specific language governing permissions and
  limitations under the License.

-->
<mx:Application  width='800' height='800' xmlns:mx="http://www.adobe.com/2006/mxml" creationComplete="startRun()">

    
    <mx:Script>
    <![CDATA[
    	import mx.messaging.config.ServerConfig;
    	import mx.messaging.Channel;
    	import mx.messaging.ChannelSet;
        import mx.messaging.Producer;
        import mx.messaging.Consumer;
        import mx.events.PropertyChangeEvent;
        import mx.rpc.events.*;
        import qa.mxunit.Assert;
        import qa.mxunit.MXUnitManager;
        import mx.messaging.messages.*;
        import mx.messaging.events.*;   
        
        //--------------------------------------------------------------------------
        //
        // Variables
        // 
        //--------------------------------------------------------------------------
        private var con:Consumer;
        private var pro:Producer;
        private var destination:String = "topic_auth_subscribe";
        private var timer:Timer;
        private var cs:ChannelSet;
        private var ch:Channel;

        //--------------------------------------------------------------------------
        //
        // Setup 
        // 
        //--------------------------------------------------------------------------
        /**
         * @private
         */
        private function startRun():void {        
        	cs = new ChannelSet(); 
            ch = ServerConfig.getChannel("qa-http-polling"); 
            cs.addChannel(ch);                               
            MXUnitManager.getInstance().addTests(this,["testConsumerNoCredentials","testConsumerBadCredentials","testConsumerNoCredProducerNoCred","testConsumerValidCredentials"],10000);                                    
        }
        //--------------------------------------------------------------------------
        //
        // Test Methods 
        // 
        //--------------------------------------------------------------------------              
        
        //testConsumerNoCredentials
        /**
         * Test that a consumer that subscribes to a destination that 
         * uses a subscribe-security-constraint and doesn't set any credendtials
         * gets an error.  
         */         
        public function testConsumerNoCredentials():void {                        
            con = new Consumer(); 
            con.channelSet = cs; 
            con.destination = destination; 
            MXUnitManager.getInstance().currentTest.chain(con,[MessageFaultEvent.FAULT],conNoCredEventHandler);
            con.subscribe();
        }
        /**
         * @private 
         * Handle message fault event that is returned when consumer with no credentials
         * subscribes to a destination with a subscribe-security-constraint. 
         */
        private function conNoCredEventHandler(e:Event):void {                                
            if (e is MessageFaultEvent) 
            {               
                var mfe:MessageFaultEvent = e as MessageFaultEvent;
                var errorMessage:String = "Wrong faultCode, should have been either Client.Authentication or Channel.Authentication.Error"; 
                Assert.assertTrue(errorMessage, mfe.faultCode == "Client.Authentication" || mfe.faultCode == "Channel.Authentication.Error");
                //Assert.assertEquals("Wrong faultCode", "Client.Authentication", mfe.faultCode);
                //Assert.assertEquals("Wrong faultString", "Login required before authorization can proceed.", mfe.faultString);
                Assert.assertFalse("Consumer should not be subscribed",con.subscribed);                
            } 
                                                                   
        }      
        //testConsumerBadCredentials
        /**
         * Test that a consumer that subscribes to a destination that 
         * uses a subscribe-security-constraint and uses an invalid set of credentials
         * gets an error. 
         */         
        public function testConsumerBadCredentials():void {                        
            con = new Consumer(); 
            con.channelSet = cs; 
            con.destination = destination;
            con.setCredentials("sampleuser","foo"); 
            MXUnitManager.getInstance().currentTest.chain(con,[MessageFaultEvent.FAULT],conBadCredEventHandler);
            con.subscribe();
        }
        /**
         * @private
         */
        private function conBadCredEventHandler(mfe:MessageFaultEvent):void {    
            var errorMessage:String = "Wrong faultCode, should have been either Client.Authentication or Channel.Authentication.Error"; 
            Assert.assertTrue(errorMessage, mfe.faultCode == "Client.Authentication" || mfe.faultCode == "Channel.Authentication.Error");
            //Assert.assertEquals("Wrong faultCode", "Client.Authentication", mfe.faultCode);
            //Assert.assertEquals("Wrong faultString", "Invalid login.", mfe.faultString);       
            Assert.assertFalse("Consumer should not be subscribed",con.subscribed);     
        }  
        //testConsumerValidCredentials              
       /**
        * Test that a Consumer that subscribes to a destination that 
        * uses a subscribe-security-constraint and uses a valid set of credentials
        * can receive a message from a producer.
        */  
        public function testConsumerValidCredentials():void {                        
            con = new Consumer(); 
            con.channelSet = cs; 
            con.destination = destination; 
            con.setCredentials("sampleuser","samplepassword");
            MXUnitManager.getInstance().currentTest.chain(con,[MessageAckEvent.ACKNOWLEDGE,MessageFaultEvent.FAULT],conValidCredEventHandler);
            con.subscribe();
            
            pro = new Producer();
            pro.channelSet = cs; 
            pro.destination = destination;
        }
        /**
         * @private
         */
        private function conValidCredEventHandler(e:Event):void {
            if (e is MessageFaultEvent) 
            {
                var mfe:MessageFaultEvent = e as MessageFaultEvent;
                Assert.fail("Credentials were valid, shouldn't have received fault: " + mfe.faultCode);      
            } else if (e is MessageAckEvent) 
            {
                var mae:MessageAckEvent = e as MessageAckEvent;
                if((mae.correlation is CommandMessage) && (CommandMessage(mae.correlation).operation == CommandMessage.SUBSCRIBE_OPERATION))
                {
                    MXUnitManager.getInstance().currentTest.chain(con,[MessageEvent.MESSAGE],conValidCredMessageHandler);
                    var msg:IMessage = new AsyncMessage();
                    msg.body = "hello";
                    pro.send(msg);                  
                } 
            }                                       
        } 
        private function conValidCredMessageHandler(e:MessageEvent):void {            
            Assert.assertEquals("Wrong message, should have been 'hello' but was " + e.message.body.toString(), "hello", e.message.body.toString());    
            con.logout();
        }
        //testConsumerNoCredProducerNoCred  
       /**
        * Test that a producer can send a message to a destination to a destination that uses a 
        * send-security-constraint without supplying credentials and that a consumer gets challenged.
        */  
        public function testConsumerNoCredProducerNoCred():void {                        
            con = new Consumer(); 
            con.channelSet = cs; 
            con.destination = destination;
            pro = new Producer(); 
            pro.channelSet = cs;
            pro.destination = destination;    
            timer = new Timer(2000);        
            pro.addEventListener(MessageFaultEvent.FAULT,conNoCredProNoCredEventHandler);
            MXUnitManager.getInstance().currentTest.chain(timer,[TimerEvent.TIMER],conNoCredProNoCredEventHandler);
            MXUnitManager.getInstance().currentTest.chain(con,[MessageFaultEvent.FAULT],conNoCredProNoCredFaultHandler);            
            var message:AsyncMessage = new AsyncMessage(); 
            message.body = "hello";
            pro.send(message);
            con.subscribe();
            timer.start();
            
        }
        /**
         * @private
         */
        private function conNoCredProNoCredEventHandler(e:Event):void {
            if (e is MessageFaultEvent)
            {
                var mfe:MessageFaultEvent = e as MessageFaultEvent;      
                Assert.fail("No send-security-constraint, producer shouldn't have faulted" + mfe.faultCode);
            } else if (e is TimerEvent) {
                //remove method from the addAsyc queue
                timer.stop();
                timer.removeEventListener(TimerEvent.TIMER, conNoCredProNoCredEventHandler);    
            }
                                              
        }
        private function conNoCredProNoCredFaultHandler(e:MessageFaultEvent):void {
            var errorMessage:String = "Wrong faultCode, should have been either Client.Authentication or Channel.Authentication.Error"; 
            Assert.assertTrue(errorMessage, e.faultCode == "Client.Authentication" || e.faultCode == "Channel.Authentication.Error");
            //Assert.assertEquals("Wrong faultCode", "Client.Authentication", e.faultCode);
            //Assert.assertEquals("Wrong faultString", "Login required before authorization can proceed.", e.faultString);
            Assert.assertFalse("Consumer should not be subscribed",con.subscribed);                               
        }
       
    ]]>
    </mx:Script>
</mx:Application>