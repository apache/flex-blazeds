<?xml version="1.0" encoding="utf-8"?>
<!--

  Licensed to the Apache Software Foundation (ASF) under one or more
  contributor license agreements.  See the NOTICE file distributed with
  this work for additional information regarding copyright ownership.
  The ASF licenses this file to You under the Apache License, Version 2.0
  (the "License"); you may not use this file except in compliance with
  the License.  You may obtain a copy of the License at

      http://www.apache.org/licenses/LICENSE-2.0

  Unless required by applicable law or agreed to in writing, software
  distributed under the License is distributed on an "AS IS" BASIS,
  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
  See the License for the specific language governing permissions and
  limitations under the License.

-->
<mx:Application xmlns:mx="http://www.adobe.com/2006/mxml" xmlns="*" creationComplete="runTest()" height="100%" width="100%" >

    <mx:Consumer id="consumer" destination="NonDynamicDestinationAllowSubtopicsFalseAMF"
        message="messageHandler(event)"
        fault="faultHandler(event, 'consumer')"
        acknowledge="ackHandler(event, 'consumer')"
        channelConnect="connectedHandler(event, 'consumer')"
        channelDisconnect="disconnectedHandler(event, 'consumer')"/>

    <mx:Producer id="producer" destination="NonDynamicDestinationAllowSubtopicsFalseAMF"
        fault="faultHandler(event, 'producer')"
        acknowledge="ackHandler(event, 'producer')"
        channelConnect="connectedHandler(event, 'producer')"
        channelDisconnect="disconnectedHandler(event, 'producer')"/>

    <mx:Script>
        <![CDATA[
        import mx.messaging.events.MessageAckEvent;
        import qa.mxunit.Assert;
        import qa.mxunit.MXUnitManager;

        import mx.messaging.events.ChannelEvent;
        import mx.messaging.events.MessageEvent;
        import mx.messaging.events.MessageFaultEvent;
        import mx.messaging.messages.AsyncMessage;


        public var producerEvents:Array = [];
        public var consumerEvents:Array = [];
        public var messages:Array = [];
        public var testsStarted:Boolean = false;

        ////////////////////////////////////////////////////////////////////////////
        //
        // Setup method.
        //
        //////////////////////////////////////////////////////////////////////////// 
        
        /**
        *  This setup method does all of the testing work and the test methods just verify
        *  the outcome. Be careful not to remove the delay on the MXUnitManager otherwise the 
        *  test methods will be called to verify the results before the test work has finished.   
        */
        public function runTest():void {
            consumer.subtopic = "foo.bar.baz";
            consumer.subscribe();

            var message:AsyncMessage = new AsyncMessage();
            message.body = "if you can read this then there's a bug";
            message.headers = {from:"t0m"};

            producer.subtopic = "foo.bar.baz";
            producer.send(message);

            // Just in case no event handlers fire, make sure we start the
            // tests after 10 secs
            var timer:Timer = new Timer(10000, 1);
            timer.addEventListener("timer", startMyTests);
            timer.start();
        }
        ////////////////////////////////////////////////////////////////////////////
        //
        // Tests.
        //
        //////////////////////////////////////////////////////////////////////////// 
        /**
        * Test that when a producer specifies a subtopic and tries to send a message to a 
        * destination that has allow subtopics set to false that the producer receives 
        * a fault.
        */ 
        public function testProducerFault():void {
            consumer.unsubscribe();
            Assert.assertContains("Fault not found", "does not allow subtopics", producerEvents.toString());
        }
        /**
        * Test that when a consumer specifies a subtopic and tries to subscribe to a 
        * destination that explicitly has allow subtopics set to false that the consumer 
        * receives a fault.
        */ 
        public function testConsumerFault():void {
            Assert.assertContains("Fault not found", "does not allow subtopics", consumerEvents.toString());
            Assert.assertEquals("messages length (" + messages.toString() + ")", 0, messages.length);
        }
        private function messageHandler(event:MessageEvent):void {
            log(String(event.message.headers.from + ": " + event.message.body));
            messages.push(event.message.headers.from + ": " + event.message.body);

            startMyTests();
        }

        private function startMyTests(e:Event=null):void
        {
            if (!testsStarted)
            {
                testsStarted = true;
                MXUnitManager.detectTests(this, 10000);
            }
        }

        private function faultHandler(event:MessageFaultEvent, whose:String):void {
            log("*** " + whose + " fault: " + event.faultCode + " : " + event.faultString + " ***");
            this[whose + "Events"].push("fault: " + event.faultCode + " : " + event.faultString);

            startMyTests();
        }

        private function connectedHandler(event:ChannelEvent, whose:String):void {
            log(' - ' + whose + ' channelConnect.');
            this[whose + "Events"].push("channelConnect");
        }

        private function disconnectedHandler(event:ChannelEvent, whose:String):void {
            log(' - ' + whose + ' channelDisconnect.');
            this[whose + "Events"].push("channelDisconnect");
        }

        private function ackHandler(event:MessageEvent, whose:String):void {
            log(' - ' + whose + ' ack.');
            this[whose + "Events"].push("ack");

            startMyTests();
        }

        private function log(message:String):void {
            output.text += message + "\n";
        }       

        ]]>
    </mx:Script>

    <mx:TextArea id="output" height="300" width="500" editable="false" />

</mx:Application>